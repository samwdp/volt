fn append_query(target: &mut String, query: &str) {
    if !target.is_empty() {
        target.push('\n');
    }
    target.push_str(query.trim());
}

pub(crate) fn javascript_extra_highlight_query(include_jsx: bool) -> String {
    let mut query = String::new();
    append_query(&mut query, JAVASCRIPT_PARAMS_HIGHLIGHT_QUERY);
    if include_jsx {
        append_query(&mut query, JSX_HIGHLIGHT_QUERY);
    }
    query
}

pub(crate) fn typescript_extra_highlight_query(include_jsx: bool) -> String {
    let mut query = String::new();
    append_query(&mut query, JAVASCRIPT_BASE_HIGHLIGHT_QUERY);
    append_query(&mut query, TYPESCRIPT_PARAMS_HIGHLIGHT_QUERY);
    if include_jsx {
        append_query(&mut query, JSX_HIGHLIGHT_QUERY);
    }
    query
}

const JAVASCRIPT_BASE_HIGHLIGHT_QUERY: &str = r####"
; Variables
;----------

(identifier) @variable

; Properties
;-----------

(property_identifier) @property

; Function and method definitions
;--------------------------------

(function_expression
  name: (identifier) @function)
(function_declaration
  name: (identifier) @function)
(method_definition
  name: (property_identifier) @function.method)

(pair
  key: (property_identifier) @function.method
  value: [(function_expression) (arrow_function)])

(assignment_expression
  left: (member_expression
    property: (property_identifier) @function.method)
  right: [(function_expression) (arrow_function)])

(variable_declarator
  name: (identifier) @function
  value: [(function_expression) (arrow_function)])

(assignment_expression
  left: (identifier) @function
  right: [(function_expression) (arrow_function)])

; Function and method calls
;--------------------------

(call_expression
  function: (identifier) @function)

(call_expression
  function: (member_expression
    property: (property_identifier) @function.method))

; Special identifiers
;--------------------

((identifier) @constructor
 (#match? @constructor "^[A-Z]"))

([
    (identifier)
    (shorthand_property_identifier)
    (shorthand_property_identifier_pattern)
 ] @constant
 (#match? @constant "^[A-Z_][A-Z\\d_]+$"))

((identifier) @variable.builtin
 (#match? @variable.builtin "^(arguments|module|console|window|document)$")
 (#is-not? local))

((identifier) @function.builtin
 (#eq? @function.builtin "require")
 (#is-not? local))

; Literals
;---------

(this) @variable.builtin
(super) @variable.builtin

[
  (true)
  (false)
  (null)
  (undefined)
] @constant.builtin

(comment) @comment

[
  (string)
  (template_string)
] @string

(regex) @string.special
(number) @number

; Tokens
;-------

[
  ";"
  (optional_chain)
  "."
  ","
] @punctuation.delimiter

[
  "-"
  "--"
  "-="
  "+"
  "++"
  "+="
  "*"
  "*="
  "**"
  "**="
  "/"
  "/="
  "%"
  "%="
  "<"
  "<="
  "<<"
  "<<="
  "="
  "=="
  "==="
  "!"
  "!="
  "!=="
  "=>"
  ">"
  ">="
  ">>"
  ">>="
  ">>>"
  ">>>="
  "~"
  "^"
  "&"
  "|"
  "^="
  "&="
  "|="
  "&&"
  "||"
  "??"
  "&&="
  "||="
  "??="
] @operator

[
  "("
  ")"
  "["
  "]"
  "{"
  "}"
] @punctuation.bracket

(template_substitution
  "${" @punctuation.special
  "}" @punctuation.special) @embedded

[
  "as"
  "async"
  "await"
  "break"
  "case"
  "catch"
  "class"
  "const"
  "continue"
  "debugger"
  "default"
  "delete"
  "do"
  "else"
  "export"
  "extends"
  "finally"
  "for"
  "from"
  "function"
  "get"
  "if"
  "import"
  "in"
  "instanceof"
  "let"
  "new"
  "of"
  "return"
  "set"
  "static"
  "switch"
  "target"
  "throw"
  "try"
  "typeof"
  "var"
  "void"
  "while"
  "with"
  "yield"
] @keyword
"####;

const JAVASCRIPT_PARAMS_HIGHLIGHT_QUERY: &str = r####"
(formal_parameters
  [
    (identifier) @variable.parameter
    (array_pattern
      (identifier) @variable.parameter)
    (object_pattern
      [
        (pair_pattern value: (identifier) @variable.parameter)
        (shorthand_property_identifier_pattern) @variable.parameter
      ])
  ]
)
"####;

const TYPESCRIPT_PARAMS_HIGHLIGHT_QUERY: &str = r####"
[
  (required_parameter
    [
      (array_pattern
        (identifier) @variable.parameter)
      (object_pattern
        [
          (pair_pattern value: (identifier) @variable.parameter)
          (shorthand_property_identifier_pattern) @variable.parameter
        ])
      (rest_pattern
        (identifier) @variable.parameter)
    ])
  (optional_parameter
    [
      (array_pattern
        (identifier) @variable.parameter)
      (object_pattern
        [
          (pair_pattern value: (identifier) @variable.parameter)
          (shorthand_property_identifier_pattern) @variable.parameter
        ])
      (rest_pattern
        (identifier) @variable.parameter)
    ])
]
"####;

const JSX_HIGHLIGHT_QUERY: &str = r####"
(jsx_opening_element (identifier) @tag (#match? @tag "^[a-z][^.]*$"))
(jsx_closing_element (identifier) @tag (#match? @tag "^[a-z][^.]*$"))
(jsx_self_closing_element (identifier) @tag (#match? @tag "^[a-z][^.]*$"))

(jsx_attribute (property_identifier) @attribute)
(jsx_opening_element (["<" ">"]) @punctuation.bracket)
(jsx_closing_element (["</" ">"]) @punctuation.bracket)
(jsx_self_closing_element (["<" "/>"]) @punctuation.bracket)
"####;
